plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '5.0.0'
    id 'org.sonarqube' version '2.6'
}

group = 'io.finstats'
version = '1.0-SNAPSHOT'

apply plugin: 'application'
apply plugin: 'idea'
apply plugin: 'jacoco'

repositories {
    mavenCentral()
}

application {
    mainClassName = 'io.vertx.core.Launcher'
}

sourceCompatibility = '1.8'

def mainVerticleName = 'io.finstats.FinStatsVerticle'
def watchForChange = 'src/**/*'
def doOnChange = './gradlew classes'

jar {
    manifest {
        attributes 'Implementation-Title': 'finstats',
                   'Implementation-Version': version
    }
}

ext {
    vertxVersion = '3.6.3'
    slf4jVersion = '1.7.26'
}

dependencies {

    compileOnly 'org.projectlombok:lombok:1.18.6'
    annotationProcessor 'org.projectlombok:lombok:1.18.6'

    compile "io.vertx:vertx-core:$vertxVersion"
    compile "io.vertx:vertx-web:$vertxVersion"

    compile "org.slf4j:slf4j-api:$slf4jVersion"
    compile "org.slf4j:log4j-over-slf4j:$slf4jVersion"
    compile "org.slf4j:jul-to-slf4j:$slf4jVersion"

    compile "ch.qos.logback:logback-classic:1.1.6"

    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'io.rest-assured:rest-assured:3.3.0'
}

build.mustRunAfter clean

shadowJar {
    classifier = 'fat'
    manifest {
        attributes 'Main-Verticle': mainVerticleName
    }
    mergeServiceFiles {
        include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
    }
}

run {
    args = ['run', mainVerticleName, "--redeploy=$watchForChange", "--launcher-class=$mainClassName", "--on-redeploy=$doOnChange"]
}
